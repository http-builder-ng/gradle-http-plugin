plugins {
    id 'com.github.hierynomus.license' version '0.13.1'
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'com.gradle.plugin-publish' version '0.9.7'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id 'maven-publish'
    id 'com.stehno.gradle.webpreview' version '0.2.0'
    id 'org.asciidoctor.convert' version '1.5.3'
    id 'com.stehno.gradle.site' version '0.0.3'
}

version = '0.0.1'
group = 'io.github.http-builder-ng'

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    jcenter()
}

configurations {
    groovyDoc
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    groovyDoc 'org.codehaus.groovy:groovy-groovydoc:2.4.12'
    groovyDoc 'org.codehaus.groovy:groovy-ant:2.4.12'

    // TODO: ideally the plugin would download the desired client only
    compile("io.github.http-builder-ng:http-builder-ng-apache:$httpBuilderVersion") {
        exclude module: 'groovy-all'
    }
    compile("io.github.http-builder-ng:http-builder-ng-okhttp:$httpBuilderVersion") {
        exclude module: 'groovy-all'
    }

    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile('com.stehno.ersatz:ersatz:1.5.0') {
        exclude module: 'groovy-all'
        exclude group: 'ch.qos.logback'
    }
}

test {
    maxParallelForks = 4
}

groovydoc {
    groovyClasspath = configurations.groovyDoc
}

asciidoctor {
    options doctype: 'book'

    backends = ['html5']

    attributes 'source-highlighter': 'coderay',
        'coderay-linenums-mode': 'table',
        icon: 'font',
        linkattrs: true,
        encoding: 'utf-8'
}

license {
    header rootProject.file('config/license_header.txt')
    ext.year = Calendar.instance.get(Calendar.YEAR)
}

gradlePlugin {
    plugins {
        httpPlugin {
            id = 'io.github.http-builder-ng.http-plugin'
            implementationClass = 'io.github.httpbuilderng.http.HttpPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/http-builder-ng/gradle-http-plugin'
    vcsUrl = 'https://github.com/http-builder-ng/gradle-http-plugin'
    description = 'Gradle plugin to aid in creating HTTP calls as tasks.'
    tags = ['gradle', 'groovy']

    plugins {
        httpPlugin {
            id = 'io.github.http-builder-ng.http-plugin'
            displayName = 'Gradle HTTP Plugin'
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task publishSite(type: GradleBuild, group: 'Publishing', description: 'Publishes the documentation web site.', dependsOn: ['site']) {
    buildFile = 'publish.gradle'
    tasks = ['gitPublishPush']
}

site {
    versionedFile 'src/docs/asciidoc/index.adoc'
    assetDir '**/fonts/**'
}

tasks.site.dependsOn = ['build', 'jacocoTestReport', 'groovydoc', 'asciidoctor']
